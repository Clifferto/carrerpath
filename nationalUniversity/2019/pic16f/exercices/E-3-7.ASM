errorlevel -203, -205, -302				;DISMINUIR ADVERTENCIAS MOLESTAS
LIST P=16F887
#include <P16F887.inc>
;CONFIG: MCLR OFF, WATCH-GOG OFF, OSCILADOR INTOSCIO, TODO LO DEMAS OFF
__CONFIG _CONFIG1, b'1110000011010100'

;MIENTRAS SE EJECUTA UN PROGRAMA SE LLAMA CADA 1s LA INTERRUPCION QUE ACTUALIZA EL
;CONTADOR EN EL PORTD Y EN CUALQUIER MOMENTO QUE SE PRESIONE RB0, SE DETENDRA LA CUENTA
;HASTA QUE SE PRESIONE DE NUEVAMENTE

cuenta EQU 0X20							;PARA ALMACENAR EL VALOR DE LA CUENTA				
T1CON_temp EQU 0X21						;PARA GUARDAR EL ESTADO DEL TMR1

;CARGAS PARA EL RETARDO DE 1s
#define carga_TMR1H_1s MOVLW b'11100000'
#define carga_TMR1L_1s MOVLW b'11000000'

ORG 0 
GOTO setup
ORG 4
GOTO INT
ORG 5
setup			CLRF PORTB				;INICIALIZACIONES
				CLRF PORTD
				CLRF cuenta
				CLRF T1CON_temp			
				BANKSEL OSCCON			
				MOVLW b'10001010'		;SE UTILIZA EL OSC INTERNO A 31KHz 
				MOVWF OSCCON
				BANKSEL ANSEL
				CLRF ANSELH				;PORTB A DIGITAL
				BANKSEL TRISD
				CLRF TRISD				;RB0 ENTRADA Y PORTD COMO SALIDA
				BANKSEL T1CON
				CALL TMR1_setup			;CONFIG DEL TMR1 PARA 1s
				BANKSEL OPTION_REG
				BCF OPTION_REG,INTEDG	;INT EXT POR FLANCO DESCENDENTE
				BANKSEL PORTD
				BSF INTCON,INTE			;HABILITA INTERRUPCIONES POR PERISFERICOS, EXTERNA 
				BSF INTCON,PEIE			;LA GLOBAL Y COMIENZA LA CUENTA DEL TMR1
				BSF INTCON,GIE
				BSF T1CON,TMR1ON

main			GOTO $					;ALGO

INT				BTFSC INTCON,INTF		;LLAMO LA INT EXTERNA?
				CALL INT_EXT
				BTFSC PIR1,TMR1IF		;SE CUMPLIO 1 seg?
				CALL INT_TMR1
				RETFIE

INT_EXT			MOVF T1CON,W			;GUARDAR EL ESTADO ACTUAL DEL TMR1 
				MOVWF T1CON_temp		;PARA HACER CAMBIOS
				BTFSC T1CON_temp,0		;SI ESTA PRENDIDO, LO APAGA Y VICEVERSA
				BCF T1CON,TMR1ON
				BTFSS T1CON_temp,0
				BSF T1CON,TMR1ON		
				BCF INTCON,INTF			;BAJAR BANDERA Y SALIR
				RETURN

INT_TMR1		BCF T1CON,TMR1ON		;CADA SEGUNDO PASA LA cuenta A 7SEG Y 
				CALL tablaBcd7seg		;LO MUESTRA EN PORTD
				MOVWF PORTD
				INCF cuenta,F			;ADEMAS INCREMENTA cuenta CUIDANDO QUE 
				MOVLW 0X0A				;QUE NO LLEGUE A 10
				XORWF cuenta,W
				BTFSC STATUS,Z
				CLRF cuenta
				carga_TMR1H_1s			;RECARGA TMR1 PARA 1s
				MOVWF TMR1H
				carga_TMR1L_1s
				MOVWF TMR1L
				BCF PIR1,TMR1IF 		;BAJAR BANDERA Y SALIR
				BSF T1CON,TMR1ON
				RETURN

tablaBcd7seg	MOVF cuenta,W			;TABLA DE BCD A 7SEG
				ADDWF PCL,F
				RETLW b'00111111'		;0 7SEG
				RETLW b'00000110'
				RETLW b'01011011'
				RETLW b'01001111'
				RETLW b'01100110'
				RETLW b'01101101'
				RETLW b'01111101'
				RETLW b'00000111'
				RETLW b'01111111'
				RETLW b'01101111'		;9 7SEG
				RETURN					

;---------------CONFIGURACIONES-----------------------------------------------
TMR1_setup		MOVLW b'10010100'		;TMR1: ps=2, OSC f=8KHz, OFF
				MOVWF T1CON
				carga_TMR1H_1s			;CARGA ÀRA 1s
				MOVWF TMR1H
				carga_TMR1H_1s
				MOVWF TMR1H
				BANKSEL PIE1
				BSF PIE1,TMR1IE			;HABILITAR INT POR TMR1
				RETURN
END	