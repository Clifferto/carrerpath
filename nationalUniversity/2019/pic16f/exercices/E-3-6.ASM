errorlevel -203, -205, -302				;DISMINUIR ADVERTENCIAS MOLESTAS
LIST P=16F887
#include <P16F887.inc>
;CONFIG: MCLR OFF, WATCH-GOG OFF, OSCILADOR INTOSCIO, TODO LO DEMAS OFF
__CONFIG _CONFIG1, b'1110000011010100'

;EL PROGRAMA SETEA EL MODO DE OPERACION, Y LA INTERRUPCION POR TMR0 ACTUA SEGUN EL MODO DE OPERACION CADA 1 SEGUNDO
;ADEMAS LA INT EXTERNA SETEA EL MODO DE OPERACION EN PARPADEO EN CUALQUIER MOMENTO

CBLOCK 0X20
	modo								;MODO DE OPERACION: modo = 0X00 -> PARPADEO 
	aux									;					     = 0X01 -> DESP DER 			
ENDC									;						 = 0X02 -> DESP IZQ

ORG 0
GOTO setup
ORG 4
GOTO INT
ORG 5
setup			CLRF PORTA				;INICIALIZACIONES
				CLRF PORTB
				CLRF PORTD
				CLRF modo
				MOVLW .20
				MOVWF aux				;aux = 20 PARA UN RETARDO DE 1 SEG APROX
				BANKSEL ANSEL		
				CLRF ANSEL				;PORTA Y PORTB A DIGITAL, PORTD SALIDA DE DATOS
				CLRF ANSELH
				BANKSEL TRISD
				CLRF TRISD				
				CALL TMR0_setup			;CONFIGURACION DE TMR0
				BANKSEL PORTD
				BSF INTCON,INTE			;HABILITAR INTERRUPCIONES POR TMR0 Y EXTERNA
				BSF INTCON,T0IE
				MOVLW .60				;INICIALIZAR TMR0 Y HABILITAR LAS INTERRUPCIONES GLOBALES
				MOVWF TMR0
				BSF INTCON,GIE

main			BTFSC PORTA,RA4			;SE PRESIONO RA4?
				GOTO $-1				;NO -> SEGUIR ESPERANDO
				BTFSS PORTA,RA4			;SI -> SE SOLTO RA4?
				GOTO $-1				;NO -> ESPERAR A QUE SE SUELTE
				BTFSS modo,0			;SI -> ESTA PARPADEO O DESP IZQ?
				GOTO modoDer			;SI -> PONER EN MODO DESP DER Y VOLVER
				BTFSC modo,0			;NO -> PONER EN DESP IZQ Y VOLVER
				GOTO modoIzq
				GOTO main

modoDer			CLRF modo				;modo = 0X01 = DESPLAZAMIENTO HACIA DERECHA
				BSF modo,0
				GOTO main

modoIzq			CLRF modo				;modo = 0X02 = DESPLAZAMIENTO HACIA IZQUIERDA 
				BSF modo,1
				GOTO main

INT				BTFSC INTCON,INTF		;LLAMO INT EXTERNA?
				CALL INT_EXT			;SI -> PONER EN MODO PARPADEO
				BTFSC INTCON,T0IF		;LLAMO EL TMR0?
				CALL INT_TMR0			;SI -> SI SE CUMPLIO 1 seg ACTUAR
				RETFIE

INT_EXT			CLRF modo				;modo = 0X00 = PARPADEO, LIMPIAR EL PORTD Y BAJAR BANDERA DE INT EXT
				CLRF PORTD
				BCF INTCON,INTF
				RETURN

INT_TMR0		DECFSZ aux				;SE CUMPLIO UN SEGUDO?
				GOTO S1					;NO -> RECARGA TMR0 Y SALIR	
				MOVLW .20				;SI -> INICIALIZAR NUEVAMENTE aux
				MOVWF aux
				CALL actuar				;ACTUAR SEGUN EL MODO DE OPERACION, BAJAR BANDERAS Y SALIR
S1     			BCF INTCON,T0IF			
				MOVLW .60
				MOVWF TMR0
				RETURN

actuar			MOVF modo,W				;MODO PARPADEO? -> COMPLEMENTAR PORTD
				BTFSC STATUS,Z			
				COMF PORTD
				BTFSC modo,0			;MODO DESP DER? -> DESPLAZAR A LA DERECHA
				CALL despDer
				BTFSC modo,1			;MODO DESP IZQ -> DESP IZQ
				CALL despIzq
				RETURN

despDer			MOVF PORTD,W			;SI HAY UN 0X00 o 0XFF EN PORTD, SETEAR EL PRIMER BIT PARA COMENZAR A DESPLAZAR
				BTFSC STATUS,Z
				GOTO $+7
				COMF PORTD,W
				BTFSC STATUS,Z
				GOTO $+4
				BCF STATUS,C			;SI NO, DESPLAZAR UN BIT A LA DER Y CUIDAR QUE PORTD NO QUEDE EN 0X00
				RRF PORTD,W
				BTFSC STATUS,Z
				MOVLW b'10000000'
				MOVWF PORTD
				RETURN

despIzq			MOVF PORTD,W			;SI 0X00 o 0XFF EN PORTD, SETEAR EL ULTIMO BIT
				BTFSC STATUS,Z
				GOTO $+7
				COMF PORTD,W
				BTFSC STATUS,Z
				GOTO $+4
				BCF STATUS,C			;DESPLAZAR A LA IZQUIERDA
				RLF PORTD,W
				BTFSC STATUS,Z
				MOVLW b'00000001'
				MOVWF PORTD
				RETURN	

;---------------CONFIGURACIONES--------------------------------------------------------
TMR0_setup		NOP
				BANKSEL OPTION_REG		;PULL-UPS OFF, INT POR FLANCO ASCENDENTE, PS = 256	
				MOVLW b'11010111'
				MOVWF OPTION_REG
				BANKSEL TMR0
				BCF INTCON,T0IF			;LIMPA BANDERA Y TMR0
				CLRF TMR0				
				RETURN
END							