errorlevel -203, -205, -302
LIST P=16F887
#include <P16F887.inc>
;POWER-UP-TIMER ON, WATCH-DOG OFF,OSC XT CRYSTAL 4MHz
__CONFIG _CONFIG1,B'0010000011100001'

CBLOCK 0X20
	aux
ENDC

ORG 0
GOTO setup
ORG 5
setup			CLRF PORTC				;INICIALIZACIONES
				CLRF PORTB
				CALL ADC_setup				
				CALL PWM_setup
				BANKSEL PIR1
				BSF ADCON0,ADON			;COMIENZA MUESTREO

main			CALL muestrear
				BSF ADCON0,GO			;COMIENZA CONVERSION
				BTFSS PIR1,ADIF			;TERMINO CONVERSION?
				GOTO $-1				;NO -> CONTINUA
				BCF PIR1,ADIF			;BAJA BANDERA DEL ADC
				CALL centrar
				MOVWF CCPR2L			;SETEAR NUEVO DUTY-CICLE DE 10 BITS
				GOTO main

PWM_setup		MOVLW .63				;MODULO DE CCPwm, PWM CONFIG:			
				BANKSEL PR2				
				MOVWF PR2				;TMR2 PERIODO = 63 -> f=1KHz
				BANKSEL CCP2CON
				MOVLW B'00001111'		;DUTY CLYCLE Lsb=0, MODO PWM
				MOVWF CCP2CON
				CLRF CCPR2L				;INICIA CON DUTY CYCLE DE 0%
				BANKSEL TMR2
				CLRF PIR1				;INICIALIZA BANDERA DEL TMR2
				CLRF TMR2				;INICIALIZA TMR2
				MOVLW B'00000011'		;PreS = 16, PosS = 1
				MOVWF T2CON
				BSF T2CON,TMR2ON		;TMR2 COMIENZA
				BTFSS PIR1,TMR2IF		;ESPERA UN PERIODO DE ESTABILIZACION
				GOTO $-1
				BCF PIR1,TMR2IF			;BAJA BANDERA
				BANKSEL TRISC
				BCF TRISC,TRISC1		;PWM SALIDA ACTIVADA
				RETURN

ADC_setup		MOVLW B'11110000'		;OSC RC INT, CH12, ADC OFF
				BANKSEL ADCON0
				MOVWF ADCON0
				BANKSEL ADCON1
				CLRF ADCON1				;RES Izquierda, +Vref=VCC, -Vref=GND
				BANKSEL INTCON
				CLRF INTCON				;INICIALIZA BANDERAS
				BSF INTCON,PEIE			;INTERRUPCIONES DE PERISFERICOS PARA LEVANTAR EL MICRO
				CLRF PIR1				;INICIALIZA BANDERAS DEL ADC
				BANKSEL PIE1
				BSF PIE1,ADIE			;INTERRUPCION POR ADC
				RETURN

muestrear       NOP
				NOP
				NOP
				NOP
				NOP
				RETURN	

centrar			MOVLW .127				;FUNCION: f(x) = 2|X-127|
				SUBWF ADRESH,W			;RES ADC - 127
				CALL val_abs
				BTFSC ADRESL,7			;SETEAR Lsb DEL DUTY-CICLE
				BSF CCP2CON,DC2B1
				BTFSS ADRESL,7
				BCF CCP2CON,DC2B1
				BTFSC ADRESL,6
				BSF CCP2CON,DC2B0
				BTFSS ADRESL,6
				BCF CCP2CON,DC2B0
X2				BCF STATUS,C			;LIMPIAR CARRY
				RLF aux,W				;AUX X2 GUARDADO EN W
				RETURN

val_abs			MOVWF aux
				BTFSC STATUS,Z			;ES CERO?
				RETURN					;SI -> aux = 0
				BTFSC STATUS,C			;NO -> ES POSITIVO?
				RETURN					;SI -> EL SIGNO ES CORRECTO, aux = X
				COMF aux,F				;NO -> CAMBIAR SIGNO	
				INCF aux,F
				RETURN					;aux = |-X|
END