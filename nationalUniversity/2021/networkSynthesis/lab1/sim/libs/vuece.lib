* Modified from LTspice's UniversalOpamp2.lib level.3a
* Dan White <dan.white@valpo.edu>
*
* Parameters match their usage in ECE 340 and ECE 341
*
* Fixed:
*   {Rin=>Rid} to be the differential input resistance
* Added:
*   {Ib}    - Input bias current, into pins
*   {Ios}   - Input offset current, Ib1-Ib2, split between both inputs
*             (input currents are to 0 and not included in power supply current)
*   {Is}    - DC supply current, dynamic current is already included
*
* User parameters accepted:
* -------------------------
* Avol  - open-loop gain (low frequency)
* Rid   - differential input resistance
* GBW   - gain-bandwidth product
* Vos   - input offset voltage
* Ib    - input bias current (i1 + i2)/2
* Ios   - input offset current (i1 - i2) symmetric
* Is    - power supply current (DC bias term, without loads)
* ilimit- output stage current limit
* rail  - closest output voltage can approach a supply rail
* slew  - slew rate in V/s
* phimargin - phase margin at unity gain frequency
* en    - equivalent input voltage noise density (V^2/Hz)
* enk   - voltage noise 1/f knee frequency
* in    - equivalent input current noise density (A^2/Hz)
* ink   - current noise 1/f knee frequency
*
.subckt vuopamp 1 2 3 4 5
.param Avol=1Meg Rid=1G Vos=0 Ib=0 Ios=0 GBW=10Meg phimargin=45
.param Slew=10Meg rail=0 ilimit=25m Isupply=0
.param en=0 enk=0 in=0 ink=0 incm=0 incmk=0
******************* internal ***********************
.param Rgout=100Meg ; norton resistor for vccs
.param Cout={Avol/GBW/2/pi/Rgout}
.model Q SW(Ron=10 Roff=10Meg Vt=0 Vh=-.1 Vser={Rail} ilimit={Ilimit} noiseless level=2 epsilon={Rail/10})
.param X table(phimargin,29.4,3.5,32.1,2.9,33.8,2.6,35.8,2.3,38.1,2,40.9,1.7,43.2,1.5,45.9,1.3,49.2,1.1,53.2,0.9,58.2,0.7,64.7,0.5,73,0.3,86.1,0.05)
S1 5 3 N003 5 Q
S2 4 5 5 N003 Q
A1 2 1 0 0 0 0 N002 0 OTA G={Avol/Rgout} ref={Vos} Iout={slew*Cout} Cout={Cout} en={en} enk={enk} in={in} ink={ink} incm={incm} incmk={incmk} Vhigh=1e308 Vlow=-1e308
*Gx N002 0 2 1 {Avol/Rgout}  ; alternate for other *spice's, A devices are LTspice custom undocumented
C3 5 4 1p
C4 3 5 1p
G1 0 N003 N002 0 {1/Rgout}
R2 N003 0 {Rgout} noiseless
C1 N003 0 {X*Cout/Avol}
*R3 3 2 {2*Rid} noiseless
*R4 3 1 {2*Rid} noiseless
*R5 1 4 {2*Rid} noiseless
*R6 2 4 {2*Rid} noiseless
Rid 1 2 {Rid} noiseless
Ib1 1 0 {Ib+Ios/2}
Ib2 2 0 {Ib-Ios/2}
Is  4 5 {Isupply}
G2 0 N002 4 N002 table(0 0 10 {2*slew*Cout})
G3 N002 0 N002 3 table(0 0 10 {2*slew*Cout})
R1 N002 4 {2*Rgout} noiseless
R7 3 N002 {2*Rgout} noiseless
.ends vuopamp

* vim: ft=spice
